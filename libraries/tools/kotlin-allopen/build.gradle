apply plugin: 'kotlin'
apply plugin: 'jps-compatible'

configureGradlePluginPublishing(project)

pill {
    variant = 'FULL'
}

dependencies {
    api project(':kotlin-gradle-plugin-api')
    api project(':kotlin-gradle-plugin-model')

    api kotlinStdlib()
    compileOnly project(path: ':kotlin-compiler-embeddable', configuration: 'runtimeJar')
    compileOnly project(':kotlin-allopen-compiler-plugin')

    compileOnly gradleApi()

    testApi gradleApi()
    testApi "junit:junit:4.12"

    embedded(project(":kotlin-allopen-compiler-plugin")) { transitive = false }
}

tasks.withType(project.compileKotlin.class) {
    kotlinOptions.languageVersion = "1.3"
    kotlinOptions.apiVersion = "1.3"
    kotlinOptions.freeCompilerArgs += [
            "-Xskip-prerelease-check", "-Xskip-runtime-version-check", "-Xsuppress-version-warnings"
    ]
}

jar {
    manifestAttributes(manifest, project)
}

ArtifactsKt.noDefaultJar(project)
ArtifactsKt.runtimeJar(project, EmbeddableKt.rewriteDefaultJarDepsToShadedCompiler(project, {}), {})
configureSourcesJar()
configureJavadocJar()

pluginBundle {
    plugins {
        kotlinAllopenPlugin {
            id = 'org.jetbrains.kotlin.plugin.allopen'
            description = displayName = 'Kotlin All Open compiler plugin'
        }
        kotlinSpringPlugin {
            id = 'org.jetbrains.kotlin.plugin.spring'
            description = displayName = 'Kotlin Spring compiler plugin'
        }
    }
}

configurePluginMarkers(project)
